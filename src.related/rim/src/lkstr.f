      LOGICAL FUNCTION LKSTR(DS,DL,MS,ML)
      INCLUDE 'syspar.d'
C
C     SCANS FOR A CHARACTER STRING IN DS THAT IS 'LIKE' THE
C     CHARACTER STRING MS. MS MAY CONTAIN 'WILD-CARD' CHARACTERS.
C     (BOTH STRINGS ARE ASCII-TEXT)
C
C        DS - DESTINATION STRING
C        DL - THE NUMBER OF CHARACTERS IN DS
C        MS - STRING TO MATCH (CONTAINS WILD-CARD CHARS)
C        ML - THE NUMBER OF CHARACTERS IN MS
C
C        LSTRNG = TRUE IF MATCH FOUND
C
      INCLUDE 'flags.d'
      INCLUDE 'ascpar.d'
C
C     WILD-CARD CHARS: ARBCHS - MATCH SINGLE ARBITRARY CHARACTER
C                      ARBCHM - MATCH MULTIPLE ARBITRARY CHARACTERS
C
C
C     LOOK FOR EACH SUBSTRING OF MS IN DS
C
      DP = 1
      MP = 0
C
100   SMP = MP + 1
110   MP = MP + 1
      IF (MP.GT.ML) GOTO 200
      CALL GETT(MS,MP,A)
      IF (A.EQ.ARBCHM) GOTO 200
      GOTO 110
C
C     SUBSTRING OF MS FOUND (START=SMP, END=MP-1)
C
200   SML = MP - SMP
      IF (SML.EQ.0) GOTO 300
C
C     CHECK FOR STARTING AND ENDING STRINGS (SMP=1 OR MP>ML)
C
      IF (SMP.EQ.1) THEN
         IF (MP.GT.ML .AND. DL.NE.ML) GOTO 800
         SDL = SML
      ELSE
         IF (MP.GT.ML) DP = DL - SML + 1
         SDL = DL - DP + 1
      ENDIF
      IF (DP.LE.0) GOTO 800
      IF (DP.GT.DL) GOTO 800
      IF (DP+SDL-1.GT.DL) GOTO 800
C
C     LOOK FOR THE SUBSTRING
C
      SDP = LSTRNG(DS,DP,SDL,MS,SMP,SML)
      IF (SDP.LE.0) GOTO 800
C
C     SUBSTRING FOUND IN DS
C
      DP = SDP + SML
300   IF (MP.LE.ML) GOTO 100
C
C     MATCH
C
      LKSTR = .TRUE.
      RETURN
C
C     NO MATCH
C
800   LKSTR = .FALSE.
      RETURN
      END
