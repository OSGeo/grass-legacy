      SUBROUTINE SWCON(BUFFER,LBUF,INFIL,OUTFIL)
      INCLUDE 'syspar.d'
C
C  PURPOSE  CONTROLLING ROUTINE FOR SORT
C
C  METHOD   ROUTINE DETERMINES WHICH KIND
C           OF SORT IS REQUIRED AND CALLS
C           APPLICABLE ROUTINE TO CARRY OUT SORT
C           THE 4 TYPES OF SORT THAT ARE AVAILABLE ARE
C
C           INCORE,LINK LIST (HART)
C           INCORE,IN SITU POINTERS
C           OUT-OF-CORE,FIXED TUPLE SIZE
C           OUT-OF-CORE,VARIABLE TUPLE SIZE
C           INCORE SORT IS FIXED OR VARIABLE
C           LTUPLE TUPLES
C
C  TIMING   UNKNOWN
C
C  DEFINITION OF VARIABLES
C
C  INFIL   UNIT CONTAINING INPUT TUPLES
C          EACH TUPLE IS WRITTEN AS A
C          RECORD AS FOLLOWS
C          FOR FIXED LENGTH RECORDS
C            WRITE(INFIL) (TUP(I),I=1,LTUPLE)
C          FOR VARIABLE LENGTH RECORDS
C            WRITE(INFIL) L,(TUP(I),I=1,L)
C
C  OUTFIL  UNIT TO CONTAIN SORTED TUPLES
C          OUTFIL MAY EQ INFIL
C          FORMAT OF OUTFIL IS THE
C          SAME AS THAT OF INFIL
C
      INCLUDE 'srtcom.d'
C
C
      INTEGER BUFFER(1)
C
      LPRU = ZFSORT
      DPRU = -1
 
C
      REWIND INFIL
      I1 = 2*NSORT + 12
      IF(NSORT .GT. 2000) I1 = I1 + 89
 
      I3 = LTUPLE
      IF(FIXLT) I3 = LTUPLE*NSORT
      IF(I1+I3 .GT. LBUF) GO TO 100
C
C  INCORE SORT,HART METHOD
C
      CALL SWHART(INFIL,OUTFIL,BUFFER,I1)
      GO TO 400
C
100   IF(NSORT+I3 .GT. LBUF) GO TO 200
C
C  INCORE SORT,POINTERS IN SITU
C
      CALL SWINPO(INFIL,OUTFIL,BUFFER)
      GO TO 400
CC
C  OUT-OF-CORE SORT
C
200   IF( FIXLT) GO TO 300
C
C  VARIABLE LENGTH OUT-OF-CORE SORT
C
      CALL SWVLFS(INFIL,OUTFIL,BUFFER,LBUF,LPRU,DPRU)
      GO TO 400
C
C  FIXED TUPLE LENGTH,OUT-OF-CORE SORT
C
300   CALL SWFLFS(INFIL,OUTFIL,BUFFER,LBUF,LPRU,DPRU)
C
400   REWIND OUTFIL
      RETURN
      END
