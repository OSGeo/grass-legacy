*(    Rim report of resistance measurements by conductor )
report
 
  header
    1  5 'Resistance measurements by conductor'
    1 60 'Page: '
    1 66 page_number i5
    2 1 ' '
    3 1 'Symbol    No.    Name         Resistivity'
    4 1 ' '
  end
 
  select from conductors
    print   
      1 1 symbol a8 
      1 10 at_no i4 
      1 16 name a15 
      1 32 resistivity f6.2 
    end print   
    xsym 8 = symbol
    mline = 0
    mrest real = 0
 
    rem  select all measurements for this conductor
 
    select from measures where symbol = xsym
      mline = mline + 1
      if mline = 1  *(print the sub-header)
        print
          2 5 '   id   date               resistivity'
          3 5 '-----  --------            -----------'
        end print
      end if
      print
        1 5 id i5
        1 12 m_date 'yy/mm/dd'
        1 32 resistivity f6.2
      end print
      mrest = mrest + resistivity
    end select
 
    rem print a summary for each conductor
 
    if mline > 0
      mrest = mrest / mline
      print
        2 10 'Measured resistivity = '
        2 32 mrest f6.2
        3 1 ' '          *( one blank line following summary)
      end print
    else
      print
        2 10 'No measurements taken'
        3 1 ' '
      end print
    end if
 
    print   
      1 1 symbol a8 
      1 10 at_no i4 
      1 16 name a15 
      1 32 resistivity f6.2 
      1 50 '**repeat**'
      2 1 ' '
    end print   
  end select
 
end report
 
 
